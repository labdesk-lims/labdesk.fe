'################################################################################################
' This module provides functions to call manage the licence
'################################################################################################

Option Compare Database
Option Explicit

Public Function GetLicenceString() As Variant
On Error GoTo Catch_Error
    Dim db As database
    Dim rs As Recordset
    
    Set db = CurrentDb
    Set rs = db.OpenRecordset("SELECT TOP 1 licence As lic FROM setup")
    
    If Not rs.EOF Then GetLicenceString = rs!lic Else GetLicenceString = Null
    
Exit_Function:
    Set db = Nothing
    Set rs = Nothing
    Exit Function
Catch_Error:
    GetLicenceString = Null
    Resume Exit_Function
End Function

Public Function SetLicenceString(ByVal licence As String) As Boolean
On Error GoTo Catch_Error
    Dim db As database
    Dim rs As Recordset
    
    Set db = CurrentDb
    Set rs = db.OpenRecordset("SELECT COUNT(*) As cnt FROM setup")
    If rs!cnt > 0 Then
        db.Execute "UPDATE setup SET licence = '" & licence & "'"
    Else
        db.Execute "INSERT INTO setup (licence) VALUES('" & licence & "')"
    End If
    
    SetLicenceString = True
    
Exit_Function:
    Set db = Nothing
    Set rs = Nothing
    Exit Function
Catch_Error:
    MsgBox Err.description
    SetLicenceString = False
    Resume Exit_Function
End Function

Public Function ValidLicence(ByVal silent As Boolean) As Variant
    Dim Json As Object
    Dim s As String
    Dim rs As Recordset
    
On Error GoTo Catch_Error
    Set rs = CurrentDb.OpenRecordset("SELECT COUNT(*) As seats FROM users WHERE role IS NOT NULL")
    s = RetrieveDecryptAES(GetLicenceString, pLcSecret, 100)
    Set Json = JsonConverter.ParseJson(s)
    If Json("version") <> pReVersion Then Err.Raise vbObjectError + 513, "function valid licence", "Version does not match licence."
    If rs!seats > Json("seats") Then Err.Raise vbObjectError + 513, "function valid licence", "Maximum amount of seats reached."
    If DateValue(Json("expiry")) < Now() Then Err.Raise vbObjectError + 513, "function valid licence", "Licence expired."
    ValidLicence = True
    
Exit_Function:
    Exit Function
Catch_Error:
    If Not silent Then MsgBox Err.description, vbInformation, "Wrong licence"
    ValidLicence = False
    Resume Exit_Function
End Function