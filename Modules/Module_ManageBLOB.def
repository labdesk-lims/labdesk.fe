'################################################################################################
' This module provides file dialogues, serializes and deserializes data and writes it to the sql
' database and back to the harddisc of choice.
'################################################################################################

Option Compare Database
Option Explicit

' Global defined functions
Public Declare PtrSafe Function ShellExecute Lib "shell32.dll" Alias "ShellExecuteA" _
    (ByVal hWnd As Long, ByVal lpOperation As String, ByVal lpFile As String, _
    ByVal lpParameters As String, ByVal lpDirectory As String, _
    ByVal nShowCmd As Long) As Long
    
Public Function GetFileName(ByVal strFile As String) As String
    Dim fso As New FileSystemObject
    GetFileName = fso.GetFileName(strFile)
End Function

Public Function GetFileExtension(ByVal strFile As String) As String
    Dim fso As New FileSystemObject
    GetFileExtension = fso.GetExtensionName(strFile)
End Function

Public Function OpenFileDialog() As String
'-------------------------------------------------------------------------------
'Function:          OpenFileDialog
'Date:              2021 October
'Purpose:           Shows a File Open dialog
'Out:               File path
'-------------------------------------------------------------------------------
    'Requires reference to Microsoft Office 11.0 Object Library.
    Dim fDialog As Office.FileDialog
    Dim varFile As Variant
    
    'Set up the File Dialog.
    Set fDialog = Application.FileDialog(msoFileDialogFilePicker)
    With fDialog
        'Disallow user to make multiple selections in dialog box
        .AllowMultiSelect = False
        ' Set the title of the dialog box.
        .title = "Please select one file"
        ' Clear out the current filters, and add our own.
      .Filters.Clear
      .Filters.Add "All Files", "*.*"
      If .Show = True Then
        OpenFileDialog = GetFileName(fDialog.SelectedItems(1))
      Else
        OpenFileDialog = ""
      End If
   End With
End Function

Public Function SaveFileDialog(ByVal initialFileStr As String) As String
'-------------------------------------------------------------------------------
'Function:          SaveFileDialog
'Date:              2021 October
'Purpose:           Show a File Save dialog
'In:                File to be shown inititally
'Out:               File path where saved
'-------------------------------------------------------------------------------
    'Requires reference to Microsoft Office 11.0 Object Library.
    Dim fDialog As Office.FileDialog
    Dim varFile As Variant
    
    'Set up the File Dialog.
    Set fDialog = Application.FileDialog(msoFileDialogSaveAs)
    With fDialog
        'Disallow user to make multiple selections in dialog box
        .AllowMultiSelect = False
        .InitialFileName = initialFileStr
        ' Set the title of the dialog box.
        .title = "Save file to disk"
      If .Show = True Then
        SaveFileDialog = fDialog.SelectedItems(1)
      Else
        SaveFileDialog = ""
      End If
   End With
End Function

Public Function BlobToFile(ByVal strFile As String, ByRef field As Object) As Long
'-------------------------------------------------------------------------------
'Function:          BlobToFile
'Date:              2021 October
'Purpose:           Show a File Save dialog
'In:
'-> strFile:        Path were data will be saved to
'-> field:          Recordset field with serialized data
'Out:               Number of files processed
'-------------------------------------------------------------------------------
On Error GoTo Err_BlobToFile
    Dim nFileNum As Integer
    Dim abytData() As Byte
    BlobToFile = 0
    nFileNum = FreeFile

    Open strFile For Binary Access Write As nFileNum
    abytData = field
    Put #nFileNum, , abytData
    BlobToFile = LOF(nFileNum)

Exit_BlobToFile:
    If nFileNum > 0 Then Close nFileNum
    Exit Function

Err_BlobToFile:
    MsgBox "Function BlobToFile Error ID(" & Err.Number & "): " & Err.description & "(" & strFile & ")", vbInformation
    BlobToFile = 0
    Resume Exit_BlobToFile
End Function

Public Function FileToBlob(ByVal strFile As String, ByRef field As Object) As Boolean
'-------------------------------------------------------------------------------
'Function:          FileToBlob
'Date:              2021 October
'Purpose:           Show a File Save dialog
'In:
'-> strFile:        Path of the file to be serialized
'-> field:          Recordset field to store serialized data
'Out:               Done (T/F)
'-------------------------------------------------------------------------------
On Error GoTo Err_FileToBlob

    'Test to see if the file exists. Exit if it does not.
    If Dir(strFile) = "" Then Exit Function

    FileToBlob = True

    'Create a connection object
    Dim cn As ADODB.Connection
    Set cn = CurrentProject.Connection

    'Create our other variables
    Dim rs As ADODB.Recordset
    Dim mstream As ADODB.stream
    Set rs = New ADODB.Recordset

    'Open our Binary Stream object and load our file into it
    Set mstream = New ADODB.stream
    mstream.Open
    mstream.type = adTypeBinary
    mstream.LoadFromFile strFile

    'read our binary file into the OLE Field
    field = mstream.Read

    'Edit: Removed some cleanup code I had inadvertently left here.

CleanUp:
    On Error Resume Next
    rs.Close
    mstream.Close
    Set mstream = Nothing
    Set rs = Nothing
    Set cn = Nothing

    Exit Function

Err_FileToBlob:
    MsgBox "Function FileToBlob Error ID(" & Err.Number & "): " & Err.description, vbInformation
    FileToBlob = False
    Resume CleanUp
End Function