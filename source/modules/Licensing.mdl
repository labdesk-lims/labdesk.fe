Attribute VB_Name = "Licensing"
'################################################################################################
' This class module will activate users by licensing based on a unique identifier
' in the table users. The activation is performed manually.
'################################################################################################

Option Compare Database
Option Explicit

Public Function ActivateApplication() As Boolean
On Error GoTo Catch_Error
    Dim db As database
    Dim rs As Recordset

    Set db = CurrentDb()
    Set rs = db.OpenRecordset("SELECT cpu_id FROM dbsetup")

    If Nz(JsonConverter.ParseJson(GetLicence(GetDbSetting("licence"), GetProcessorId()))("licence_active"), False) Then
        db.Execute "UPDATE dbsetup SET cpu_id = '" & GetProcessorId() & "'"
        ActivateApplication = True
    Else
        If GetProcessorId() = Nz(rs(0), "") Then
        ActivateApplication = True
        End If
    End If
    
Exit_Function:
    Exit Function
Catch_Error:
    ActivateApplication = False
    Resume Exit_Function
End Function

Public Function GetLicence(ByVal key As String, ByVal client As String) As String
On Error GoTo Catch_Error
    Dim jsonText As String
    
    If Not PingOk("licence.labdesk.net") Then Err.Raise vbObjectError + 513, "", "Licence server unreachable."
    jsonText = httpGET("https://licence.labdesk.net/index.php/licence/get?licence_key=" & key & "&licence_client='" & client & "'")
    jsonText = Mid(jsonText, 2, Len(jsonText) - 2)
    GetLicence = jsonText
    
Exit_Function:
    Exit Function
Catch_Error:
    GetLicence = "{}"
    Resume Exit_Function
End Function
