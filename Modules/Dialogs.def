'################################################################################################
' This module provides dialogs to gather user specific informations such as files and folder
'################################################################################################

Option Compare Database
Option Explicit

Public Function OpenFileDialog() As String
'-------------------------------------------------------------------------------
'Function:          OpenFileDialog
'Date:              2021 October
'Purpose:           Shows a File Open dialog
'Out:               File path
'-------------------------------------------------------------------------------
    'Requires reference to Microsoft Office 11.0 Object Library.
    Dim fDialog As Office.FileDialog
    Dim varFile As Variant
    
    'Set up the File Dialog.
    Set fDialog = Application.FileDialog(msoFileDialogFilePicker)
    With fDialog
        'Disallow user to make multiple selections in dialog box
        .AllowMultiSelect = False
        ' Set the title of the dialog box.
        .title = "Please select one file"
        ' Clear out the current filters, and add our own.
      .Filters.Clear
      .Filters.Add "All Files", "*.*"
      If .Show = True Then
        OpenFileDialog = GetFileName(fDialog.SelectedItems(1))
      Else
        OpenFileDialog = ""
      End If
   End With
End Function

Public Function SaveFileDialog(ByVal initialFileStr As String) As String
'-------------------------------------------------------------------------------
'Function:          SaveFileDialog
'Date:              2021 October
'Purpose:           Show a File Save dialog
'In:                File to be shown inititally
'Out:               File path where saved
'-------------------------------------------------------------------------------
    'Requires reference to Microsoft Office 11.0 Object Library.
    Dim fDialog As Office.FileDialog
    Dim varFile As Variant
    
    'Set up the File Dialog.
    Set fDialog = Application.FileDialog(msoFileDialogSaveAs)
    With fDialog
        'Disallow user to make multiple selections in dialog box
        .AllowMultiSelect = False
        .InitialFileName = initialFileStr
        ' Set the title of the dialog box.
        .title = "Save file to disk"
      If .Show = True Then
        SaveFileDialog = fDialog.SelectedItems(1)
      Else
        SaveFileDialog = ""
      End If
   End With
End Function

Public Function SelectReportDialog() As Variant
'-------------------------------------------------------------------------------
'Function:          SelectReportDialog
'Date:              2023 January
'Purpose:           Show a Report Select dialog
'Out:               Name of the selected report
'-------------------------------------------------------------------------------
    Dim rpt As AccessObject, db As Object, i As Integer, s As Variant
    
    Set db = Application.CurrentProject
    
    CurrentDb.Execute "DELETE * FROM keyvalue"
    
    i = 0
    For Each rpt In db.AllReports
        If InStr(1, rpt.name, config.ReportId, vbTextCompare) = 1 Then CurrentDb.Execute "INSERT INTO keyvalue (key, value_txt) VALUES(" & i & ",'" & rpt.name & "')"
        i = i + 1
    Next rpt
    
    DoCmd.OpenForm "*SelectDialog", acNormal, , , acFormEdit, acDialog, "SELECT value_txt, key FROM keyvalue"
    
    If Not IsNull(Forms("*SelectDialog").comboBox.Column(1)) Then
        SelectReportDialog = Forms("*SelectDialog").comboBox.Column(0)
    Else
        SelectReportDialog = Null
    End If
    
    DoCmd.Close acForm, "*SelectDialog", acSaveNo
End Function